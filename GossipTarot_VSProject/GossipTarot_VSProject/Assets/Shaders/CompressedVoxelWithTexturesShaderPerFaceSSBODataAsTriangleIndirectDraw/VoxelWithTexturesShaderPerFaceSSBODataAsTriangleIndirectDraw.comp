
#version 460 core

layout (local_size_x = 512) in;

struct FaceVoxelsDataPoolMetadata {

	uint voxelDataBucketOffsetIntoMegaArrayIndex;
	uint numVoxelDataInBucket;
};

struct ChunkVoxelsDataPoolMetadata {

	uint packedChunkIndex;

	FaceVoxelsDataPoolMetadata topFaceVoxelsDataPoolMetadata;
	FaceVoxelsDataPoolMetadata bottomFaceVoxelsDataPoolMetadata;
	FaceVoxelsDataPoolMetadata leftFaceVoxelsDataPoolMetadata;
	FaceVoxelsDataPoolMetadata rightFaceVoxelsDataPoolMetadata;
	FaceVoxelsDataPoolMetadata frontFaceVoxelsDataPoolMetadata;
	FaceVoxelsDataPoolMetadata backFaceVoxelsDataPoolMetadata;
};

struct DrawElementsIndirectCommand {
    uint  count;
    uint  instanceCount;
    uint  firstIndex;
    int   baseVertex;
    uint  baseInstance;
};

layout (binding = 3, std430) buffer ChunkVoxelsDataPoolMetadatas {
	ChunkVoxelsDataPoolMetadata chunksVoxelsDataPoolMetadatas[];
};


layout (binding = 4, std430) buffer ChunksDrawElementsIndirectCommands {
	DrawElementsIndirectCommand chunksDrawElementsIndirectCommands[];
};

layout(std430, binding = 5) buffer MultiDrawIndirectDrawCount {
    uint drawCount;
};

uniform vec3 cameraPointingDirectionNormalised;
uniform vec3 worldSizeInChunks;

void WriteFaceDataToDrawCommand(in uint packedChunkIndex, inout FaceVoxelsDataPoolMetadata faceVoxelsDataPoolMetadata){

	if (faceVoxelsDataPoolMetadata.numVoxelDataInBucket > 0) {
		uint currentChunkIndirectDrawIndex = atomicAdd(drawCount, 1);

		chunksDrawElementsIndirectCommands[currentChunkIndirectDrawIndex].count = faceVoxelsDataPoolMetadata.numVoxelDataInBucket;
		chunksDrawElementsIndirectCommands[currentChunkIndirectDrawIndex].instanceCount = 1;
		chunksDrawElementsIndirectCommands[currentChunkIndirectDrawIndex].firstIndex = 0;
		chunksDrawElementsIndirectCommands[currentChunkIndirectDrawIndex].baseVertex = int(faceVoxelsDataPoolMetadata.voxelDataBucketOffsetIntoMegaArrayIndex);
		chunksDrawElementsIndirectCommands[currentChunkIndirectDrawIndex].baseInstance = packedChunkIndex;
	}
}

void main(void) {

	if (gl_GlobalInvocationID.x == 0 &&
		gl_GlobalInvocationID.y == 0 &&
		gl_GlobalInvocationID.z == 0) 
	{
		drawCount = 0;
	}

	if (gl_GlobalInvocationID.x >= (worldSizeInChunks.x * worldSizeInChunks.y * worldSizeInChunks.z)) {
		return;
	}

	float topFaceDot = dot(vec3(0, 1, 0), cameraPointingDirectionNormalised);
	float bottomFaceDot = dot(vec3(0, -1, 0), cameraPointingDirectionNormalised);
	float leftFaceDot = dot(vec3(-1, 0, 0), cameraPointingDirectionNormalised);
	float rightFaceDot = dot(vec3(1, 0, 0), cameraPointingDirectionNormalised);
	float frontFaceDot = dot(vec3(0, 0, 1), cameraPointingDirectionNormalised);
	float backFaceDot = dot(vec3(0, 0, -1), cameraPointingDirectionNormalised);

	uint currentChunkIndex = gl_GlobalInvocationID.x;
	
	bool drawAllFaces = false;

	if ((topFaceDot <= 0.52f) || drawAllFaces) {
		WriteFaceDataToDrawCommand(chunksVoxelsDataPoolMetadatas[currentChunkIndex].packedChunkIndex, chunksVoxelsDataPoolMetadatas[currentChunkIndex].topFaceVoxelsDataPoolMetadata);
	}
	if ((bottomFaceDot <= 0.52f) || drawAllFaces) {
		WriteFaceDataToDrawCommand(chunksVoxelsDataPoolMetadatas[currentChunkIndex].packedChunkIndex, chunksVoxelsDataPoolMetadatas[currentChunkIndex].bottomFaceVoxelsDataPoolMetadata);
	}
	if ((leftFaceDot <= 0.52f) || drawAllFaces) {
		WriteFaceDataToDrawCommand(chunksVoxelsDataPoolMetadatas[currentChunkIndex].packedChunkIndex, chunksVoxelsDataPoolMetadatas[currentChunkIndex].leftFaceVoxelsDataPoolMetadata);
	}
	if ((rightFaceDot <= 0.52f) || drawAllFaces) {
		WriteFaceDataToDrawCommand(chunksVoxelsDataPoolMetadatas[currentChunkIndex].packedChunkIndex, chunksVoxelsDataPoolMetadatas[currentChunkIndex].rightFaceVoxelsDataPoolMetadata);
	}
	if ((frontFaceDot <= 0.52f) || drawAllFaces) {
		WriteFaceDataToDrawCommand(chunksVoxelsDataPoolMetadatas[currentChunkIndex].packedChunkIndex, chunksVoxelsDataPoolMetadatas[currentChunkIndex].frontFaceVoxelsDataPoolMetadata);
	}
	if ((backFaceDot <= 0.52f) || drawAllFaces) {
		WriteFaceDataToDrawCommand(chunksVoxelsDataPoolMetadatas[currentChunkIndex].packedChunkIndex, chunksVoxelsDataPoolMetadatas[currentChunkIndex].backFaceVoxelsDataPoolMetadata);
	}
}

